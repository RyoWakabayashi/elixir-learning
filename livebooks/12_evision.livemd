# evision

## 依存パッケージ

```elixir
Mix.install([
  {:download, "~> 0.0.4"},
  {:evision, "~> 0.1.0-dev", github: "cocoa-xu/evision", branch: "main"},
  {:kino, "~> 0.5.2"},
  {:nx, "~> 0.1", [env: :prod, repo: "hexpm", hex: "nx", optional: true]}
])
```

```elixir
alias Evision, as: OpenCV
```

```elixir
defmodule Helper do
  def show_image(mat) do
    OpenCV.imencode!(".png", mat)
    |> IO.iodata_to_binary()
    |> Kino.Image.new(:png)
  end
end
```

## 画像のダウンロード

```elixir
# 再実行時、Download.from()でeexistエラーになるのを防止
File.rm("Lenna_%28test_image%29.png")

lenna =
  Download.from("https://upload.wikimedia.org/wikipedia/en/7/7d/Lenna_%28test_image%29.png")
  |> elem(1)
```

```elixir
lenna
|> File.read!()
|> Kino.Image.new(:jpeg)
```

## 画像の読込

```elixir
{:ok, mat} = OpenCV.imread(lenna)
```

```elixir
{:ok, size} = OpenCV.Mat.shape(mat)
```

```elixir
Helper.show_image(mat)
```

## リサイズ

```elixir
{:ok, resized_mat} = OpenCV.resize(mat, [_width = 256, _height = 128])
{:ok, {128, 256, 3}} = OpenCV.Mat.shape(resized_mat)
Helper.show_image(resized_mat)
```

## グレースケール

```elixir
lenna
|> OpenCV.imread!(flags: OpenCV.cv_IMREAD_GRAYSCALE())
|> Helper.show_image()
```

## 二値化

```elixir
{threshold, mono_mat} =
  lenna
  |> OpenCV.imread!(flags: OpenCV.cv_IMREAD_GRAYSCALE())
  |> OpenCV.threshold!(127, 255, OpenCV.cv_THRESH_BINARY())

{:ok, {512, 512}} = OpenCV.Mat.shape(mono_mat)
```

```elixir
Helper.show_image(mono_mat)
```

```elixir
lenna
|> OpenCV.imread!(flags: OpenCV.cv_IMREAD_GRAYSCALE())
|> OpenCV.threshold!(127, 255, OpenCV.cv_THRESH_BINARY())
|> elem(1)
|> Helper.show_image()
```

## 並進

```elixir
{:ok, affine} =
  [
    [1, 0, 100],
    [0, 1, 50]
  ]
  |> Nx.tensor(type: {:f, 32})
  |> OpenCV.Nx.to_mat()
```

```elixir
lenna
|> OpenCV.imread!()
|> OpenCV.warpAffine!(affine, [512, 512])
|> Helper.show_image()
```

## 回転

```elixir
{:ok, affine} = OpenCV.getRotationMatrix2D([512 / 2, 512 / 2], 90, 1)
```

```elixir
lenna
|> OpenCV.imread!()
|> OpenCV.warpAffine!(affine, [512, 512])
|> Helper.show_image()
```

## ブラー

```elixir
lenna
|> OpenCV.imread!()
|> OpenCV.blur!([9, 9])
|> Helper.show_image()
```

## メディアンブラー

```elixir
lenna
|> OpenCV.imread!()
|> OpenCV.medianBlur!(9)
|> Helper.show_image()
```

## ガウシアンブラー

```elixir
lenna
|> OpenCV.imread!()
|> OpenCV.gaussianBlur!([9, 9], 5, [])
|> Helper.show_image()
```

## 図形描画

```elixir
lenna
|> OpenCV.imread!()
|> OpenCV.rectangle!([50, 10], [125, 60], [255, 0, 0])
|> OpenCV.rectangle!([250, 60], [325, 110], [0, 255, 0], thickness: -1)
|> OpenCV.rectangle!([150, 120], [225, 320], [0, 0, 255],
  thickness: 5,
  lineType: OpenCV.cv_LINE_4()
)
|> OpenCV.ellipse!([300, 300], [100, 200], 30, 0, 360, [255, 255, 0], thickness: 3)
|> Helper.show_image()
```
